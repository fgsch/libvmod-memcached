varnishtest "Test incr_set and decr_set"

feature cmd "memcached -V"

process p1 "exec memcached -u memcached -p -1 -U 0" -start

shell {
	${vmod_topsrc}/src/tests/gen-vcl.sh ${p1_pid} \
		"--SERVER=@SERVER@ --BINARY-PROTOCOL"
}

server s1 {
	rxreq
	txresp
} -start

varnish v1 -vcl+backend {
	import memcached from "${vmod_topbuild}/src/.libs/libvmod_memcached.so";

	sub vcl_init {
		include "${tmpdir}/memcached.servers.vcl";
	}

	sub vcl_deliver {
		set resp.http.init =
		    memcached.incr_set("test05", 3, 2, 0);
		set resp.http.high =
		    memcached.incr_set("test05", 3, 2, 0);
		set resp.http.low =
		    memcached.decr_set("test05", 1, 2, 0);
	}
} -start

client c1 {
	txreq -url "/"
	rxresp
	expect resp.http.init == "2"
	expect resp.http.high == "5"
	expect resp.http.low == "4"
} -run
